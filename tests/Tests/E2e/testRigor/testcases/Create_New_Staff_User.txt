Login_and_Validate
click "Admin"
click "Users"
check that page contains "User / Groups"
click button "Add User"
check that button "Save" is visible
save "Testing#123" as "staff_pwd"
generate by template "$$$$$$$$" and save it as "generated_username"
enter saved value "generated_username" into field "rumple"
enter saved value "staff_pwd" into field "stiltskin"
generate unique name, then enter into "First Name:" and save as "gen_first_name"
generate unique name, then enter into "Last Name:" and save as "gen_last_name"
enter saved value "password" into field "adminPass"
check that button "Save" is visible
click button "Save"
// assert staff was added to list
check that page contains "User / Groups"
check that page doesn't contain button "Save"
check that table "table-responsive" at row saved value "generated_username" and column "Real Name" contains string with parameters "${gen_first_name} ${gen_last_name}"
// login as staff in a new session
start browser "Staff User" and switch
hover over the middle of the screen
check that page contains input "Username"
check that page contains input "Password"
check that page contains button "Login"
login_as_staff
validate_login
logout
// Back to admin and clean up
switch to browser "default"
wait 5 sec
hover over the middle of the screen
check that page contains "User / Groups"
check that page contains button "Add User"
click saved value "generated_username"
click checkbox "Active"
click button "Save"
// assert is not in list anymore
check that page contains "User / Groups"
check that page doesn't contain "generated_username"
switch to browser "Staff User"
wait 5 sec
hover over the middle of the screen
reload
validate_logout  // validates that user was disconnected
login_as_staff
validate_unsuccessful_login
